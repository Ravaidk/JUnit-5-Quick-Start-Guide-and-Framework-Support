group   'com.dmitrij-drandarov'
version '3.1'


ext {
    // JUnit 5
    junit5_version              = '5.0.0'
    junit5_vintage4_version     = '4.12.0'
    junit5_spring4_test_version = '1.0.0'

    // Spring 5
    spring5_version             = '5.0.0.RELEASE'
    spring5_data_jpa_version    = '2.0.0.RC2'
    // Spring 5 Boot
    spring5_boot_version        = '2.0.0.M4'

    // Spring 4
    spring4_version             = '1.11.7.RELEASE'
    // Spring 4 Boot
    spring4_boot_version        = '1.5.7.RELEASE'

    // Hibernate
    hibernate_core_version      = '5.2.10.Final'
    hibernate_jpa_version       = '1.0.0.Final'

    // Assertion
    hamcrest_version            = '1.3'
    assertj_version             = '3.8.0'

    // Mockito
    mockito_version             = '2.9.0'

    // Logging
    slf4j_version               = '1.7.25'

    // DB Drivers
    hsqldb_driver_version       = '2.4.0'
    postgresql_driver_version   = '42.1.4'
}



buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}



//------------------------------------------------------------------------------------------------------------------------
//    Configure - All Projects
//------------------------------------------------------------------------------------------------------------------------

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/snapshot' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://jitpack.io' }
    }
}

//------------------------------------------------------------------------------------------------------------------------
//    Configure - Subprojects
//------------------------------------------------------------------------------------------------------------------------

configure(subprojects - project(":03-spring")) {
    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'

    // Gradle-Configuration for JUnit 5 Platform
    junitPlatform {
        filters {
            engines {
                // include 'junit-jupiter', 'junit-vintage'
                // exclude 'custom-engine'
            }
            tags {
                // include 'incTag'
                // exclude 'excTag'
            }
            includeClassNamePattern '.*'
        }
        enableStandardTestTask true
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit5_version"
        testRuntime "org.junit.vintage:junit-vintage-engine:$junit5_vintage4_version"
    }
}



//------------------------------------------------------------------------------------------------------------------------
//    Tasks - Init project
//------------------------------------------------------------------------------------------------------------------------

import org.gradle.api.tasks.wrapper.Wrapper.DistributionType
task wrapper(type: Wrapper) {
    group 'init'
    gradleVersion '4.2'
    distributionType DistributionType.ALL
}